%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%   ***  E R O S   P R O G R A M M E   F O R   R E M O T E S  ***
?
?    =============================
?    C P - 1 - K - K   VS 930215 (PNC)              VS 91-11-21 /A-LT
?    =============================                  VS 91-01-30 /UGW
?
%     FILE: (CP)CP-1-K-K:ELAN
%
%     BASED ON GEN-2F-R  BY T.TURUNEN
%     MODULATIONS: 1X350 US     UNCODED PULSE
%                  1X(16*21) US ALTERNATING CODE PULSE   PER IPP
%                  FREQUENCY COMMUTATED OVER  - TWO FREQUENCIES FOR UNCODED
%                                             - FOUR FREQUENCIES FOR CODED
%
%     PROGRAMMED BY GUDMUND WANNBERG
%     30 JANUARY 1991,EISCAT HQ
%
%     NOTE! THE DISPLAY CONTROL INFORMATION IS CONTAINED IN THE FILE
%     (CP)CP-1-K-K:GDEF, DOPPLER-CALIBRATION IS INCLUDED AS BLOCK 2
%
%     REVISION HISTORY:
%
%     1992-05-22 All frequencies changed to avoid a potential plasma line
%                contamination problem in the D/E PP. Old frequency table
%                retained in the file for continuity of documentation/UGW
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
SPARC-TRANSFER Y
WRITE-FILE (CP)CP-1-K-K:ELAN
WRITE-FILE (CP)CP-1-K:DESC
WRITE-FILE (CP)CP-1-K-K:GDEF
WRITE-FILE (CP)CP-1-J-K:TLAN
WRITE-FILE (CP)CP-1-J-K:CLAN
%         ANTENNA POINTED PARALLEL TO MAGNETIC FIELD AT 278.6 KM ALTITUDE
%         ACCORDING TO IGRF MODEL 1985-90 FOR NOV 1986

SET-PARAMETER R1,278.6     % DEFINE INTERSECTION ALTITUDE

&K OFFSET-PPD  -418
&S OFFSET-PPD  -415
   CHANGE-REF-SITE TROMSO,,,,,
   POINT-REF-H 182.57,77.54,R1

SET-SIG-PAT ALLX
&KSET-POLAR 94,-8          %SETTINGS FOR THE NOMINAL MIDDLE POINT OF THE
&SSET-POLAR 108,-18        %REMOTE SITE VOLUME AT 277.5 KM ALTITUDE

sync -360
LOAD-CORR CP-1-J-K:CCOD    %BASED ON GEN-REMOTE; AUG.1986 VERSION
HOLD 10

LOAD-RA CP-1-J-K:TCOD
HOLD 10

TRANSFER-NOISE RAD

SET-FR 3,F7
SET-FR 4,F8
SET-FR 5,F5
SET-FR 6,F9
SET-FR 7,F6
SET-FR 8,F10

% Here follows the old frequency assignment table:

%SET-FR 3,F0
%SET-FR 4,F1
%SET-FR 5,F6
%SET-FR 6,F7
%SET-FR 7,F8
%SET-FR 8,F9

&KSET-SIG-ATT X,2            % 30/3 92 L-G V
&SSET-SIG-ATT X,1            % 10.1.90 JM

&KSET-CHAN-ATT 3,2           %BALANCE CHANNELS! LAST: 4/12 92 L-G V
&KSET-CHAN-ATT 4,1
&KSET-CHAN-ATT 5,8
&KSET-CHAN-ATT 6,5
&KSET-CHAN-ATT 7,8
&KSET-CHAN-ATT 8,6

&SSET-CHAN-ATT 3,3           % 10.1.90
&SSET-CHAN-ATT 4,2           %     JM
&SSET-CHAN-ATT 5,2
&SSET-CHAN-ATT 6,2
&SSET-CHAN-ATT 7,2
&SSET-CHAN-ATT 8,2

SYNC 10

SET-FILT 3,BU,25,,
SET-FILT 4,BU,25,,
SET-FILT 5,LI,25,,
SET-FILT 6,LI,25,,
SET-FILT 7,LI,25,,
SET-FILT 8,LI,25,,

SELECT-ALTERNATING-CODE 16

% SET UP CORRELATOR APM AND APB STACKS
% THE DETAILED OPERATIONS CAN BE SEEN FROM THE FILE "CP-1-J-K:CLAN"

SET-CORR-APB 15,15     % ALTLAGMAX, MAX. LAG INDEX IN A/C GATES
SET-CORR-APB 14, 7     % NACSG, NO. OF A/C CHANNEL SIGNAL GATES
SET-CORR-APB 13, 3     % NACBG, NO. OF A/C CHANNEL BACKGROUND+CAL. GATES
SET-CORR-APB 12,80     % ACCALPROD, THE NUMBER OF PRODUCTS/LAG IN ACACF B,C
SET-CORR-APB 11,160    % LPCALPROD, THE NUMBER OF PRODUCTS/LAG IN LPACF B,C
SET-CORR-APB 10,3      % NLPCG, NO. OF LPACF BACKGROUND+CALIBRATION GATES
SET-CORR-APB 9, 5      % NLPSG, NO. OF LPACF SIGNAL GATES
SET-CORR-APB 8,29      % LPLAGMAX, MAX. LAG INDEX IN LPACF
SET-CORR-APB 7,32      % LPSIGSAMP, NO. OF SIGNAL SAMPLES USED/ACF IN LPACF
SET-CORR-APB 6,-10     % LPMARGIN, DEFINES OVERLAP IN LPACF SIGNAL VECTOR....

SET-CORR-APB 0,1       % INCREMENT

HOLD 10

SET-CORR-APM 15, 15    % NOLAGS, THE NO. OF LAGS IN A A/C SIGNAL GATE

SET-CORR-APM 14,240    % ACSRES, A/C SIGNAL ACF GATES
SET-CORR-APM 13,345    % ACBRES, A/C BACKGROUND GATES
SET-CORR-APM 12,377    % ACCRES, A/C CALIBRATION GATES

SET-CORR-APM 11, 29    % LPMAXLAG, THE MAX. LAG INDEX OF THE LP ACFS
SET-CORR-APM 10,  0    % LPSRES, LPACF SIGNAL GATES
SET-CORR-APM  9,150    % LPBRES, LPACF BACKGR GATES
SET-CORR-APM  8,210    % LPCRES, LPACF CALIBR GATES

SET-CORR-APM  7,393    % SCANCOUNTADDRESS

SET-CORR-APM 0,1       % INCREMENT

SET-CORR-DATAIO 394    % (5+1+1+1)*30+7*15+(2+1)*16+1

SYNC 10

SET-BUF-MEM 3,   0,3   % SET UP BUFFER MAP #3
SET-BUF-MEM 4, 687,3
SET-BUF-MEM 5,1254,3
SET-BUF-MEM 6,1276,3
SET-BUF-MEM 7,2000,3
SET-BUF-MEM 8,2000,3

SET-BUF-MEM 3, 687,2   % SET UP BUFFER MAP #2
SET-BUF-MEM 4,   0,2
SET-BUF-MEM 5,1276,2
SET-BUF-MEM 6,1254,2
SET-BUF-MEM 7,2000,2
SET-BUF-MEM 8,2000,2

SET-BUF-MEM 3,   0,1   % SET UP BUFFER MAP #1
SET-BUF-MEM 4, 687,1
SET-BUF-MEM 5,2000,1
SET-BUF-MEM 6,2000,1
SET-BUF-MEM 7,1254,1
SET-BUF-MEM 8,1276,1

HOLD 10

SET-BUF-MEM 3, 687,0   % SET UP BUFFER MAP #0
SET-BUF-MEM 4,   0,0
SET-BUF-MEM 5,2000,0
SET-BUF-MEM 6,2000,0
SET-BUF-MEM 7,1276,0
SET-BUF-MEM 8,1254,0   % LAST B.M. ADDRESS USED IS 1563

SET-ADC-INT 3,10.      % 10 US SAMPLES
SET-ADC-INT 4,10.
SET-ADC-INT 5,21.      % 21 US SAMPLING INCREMENT ON CODED CHANNELS
SET-ADC-INT 6,21.
SET-ADC-INT 7,21.
SET-ADC-INT 8,21.


START-CORR

SYNC 10

START-R-C 10,1,5
SYNC 10

ENABLE-DMA U,U   
?
?   -------------------------------------------------------------------
?   TX MIGHT BE ON, CHECK DATA QUALITY. RECORDING STARTS IN 5 MINUTES.
?
?   TO START THE DISPLAY PROGRAM GIVE THE COMMAND '@RTG' FROM GRAPHICS
?   TERMINAL. FOR DOPPLER CALIBRATION GIVE 'GOTO-BLOCK 2' COMMAND
?   -------------------------------------------------------------------
?
SYNC 280

BLOCK 1
DOPPLER-CALIBRATION OFF
START-PROGRAM INTCP,,,    % START INTEGRATION PROGRAM FOR GEN-REMOTE
START-PROGRAM VELOCTY,Y,,,    % START REAL TIME VECTOR-VELOCITY PROGRAM

SET-PARAMETER I20,1        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.

DO -1
  NEW-FILE
  DO 6
    SET-PARAMETER I20,1   %TO FORCE 150-SEC INTEGRATIONS IN VELOCTY
    SYNC 150
    SET-PARAMETER I20,5
    SYNC 150
  ENDDO
ENDDO

BLOCK 2

STOP-RECORDING,,,
STOP-PROGRAM INTCP
STOP-PROGRAM VELOCTY
?
? >>>>>>>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<<<<<<
? THE DOPPLER-CALIBRATION WILL START IN 1 MIN, PLEASE MONITOR
? >>>>>>>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<<<<<<
?
WRITE-FILE (CP-EI)CP-DOPPLER:DESC
&K SET-SIGNAL-ATTENUATOR  X,10
&S SET-SIGNAL-ATTENUATOR  X,5  %changed from 25 dB 21.9.88 Jm
SET-FR  3,D
SET-FR  4,D
DOPPLER-CALIBRATION ON
SYNC 60
START-RECORDING
DO 2
?  >>> DOPPLER CALIBRATION ON <<<
? TO STOP THE PROGRAM USE "STOP-EXPERIMENT" COMMAND OR WAIT 10 MINUTES
? TO CONTINUE WITH CP-1 USE "GOTO-BLOCK 3"
?
  SYNC 300
ENDDO
DOPPLER-CALIBRATION OFF
STOP-EXPERIMENT,,,,

% STANDARD STOP OF EXPERIMENT AFTER DOPPLER-CALIBRATION

BLOCK 3
STOP-RECORDING,,,
DUMP-LOGG-FILE

SET-LO2 3,146.0      % F0
SET-LO2 4,147.0      % F2

&KSET-SIG-ATT X,4            % GaAs-FET AMPLIFIER SET-UP    2/5 89 CH 11/1 -90 KK
&SSET-SIG-ATT X,1            % 10.1.90 JM

DOPPLER-CALIBRATION OFF

?  >>> DOPPLER CALIBRATION OFF, PREPARING FOR CP-1 SETTINGS <<
?  TO START CP-1 RECORDING USE "GOTO-BLOCK 1 <HR>,<MIN>,<SEC>"
?

% WAIT HERE FOR EITHER STOP-EXPERIMENT OR GOTO-BLOCK 2 (OR 1)

DO -1
?   >>> YOU ARE IN BLOCK 3  <<<
?   >> GOTO-BLOCK 1 TO CONTINUE EXPERIMENT OR STOP-EXPERIMENT <<
SYNC 600
ENDDO

%
STOP-EXPERIMENT,,
E-O-E
