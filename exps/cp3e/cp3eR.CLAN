%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%         CORRELATOR PROGRAM GEN-REMOTE:CLAN
%
%         THIS CORRELATOR PROGRAM IS MAINLY INTENDED FOR REMOTE
%         STATION SIGNAL HANDLING AND FOR SYSTEM CALIBRATION PURPOSES.
%
%         SUBROUTINES: REMOTE1
%                      REMOTE2
%                      IMPULSE
%
%         THIS PROGRAM CAN BE USED IN SEVERAL WAYS DESCRIBED IN THE
%         REPORT : CORRELATOR PROGRAMS FOR THE GEN-SYSTEM
%
%         PROGRAM DEVELOPMENT: TAUNO TURUNEN, EISCAT HQ
%
%         VERSION MAY 1986
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%        TABLE OF INDEXES
%
%        THE INDEX TABLE RELATES THE GIVEN NAMES TO NUMBERS, WHICH
%        ARE THEN USED AS REGISTER NUMBERS. THERE IS NO DISTINCTION
%        BETWEEN THE APB AND APM STACKS
%
%        EXAMPLE: RSAPB(MARGIN1) IS RSAPB(15) AFTER COMPILATION
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
INDEX MARGIN1=15            % SUBROUTINE
INDEX SIGSAMPLES1=14        % REMOTE1
INDEX LAGMAX1=13            % APB
INDEX CALGATES1=12          % STACK
INDEX CALPRODUCTS1=11       % REGISTERS
%
INDEX MARGIN2=10            % SUBROUTINE
INDEX SIGSAMPLES2=9         % REMOTE2
INDEX LAGMAX2=8             % APB
INDEX CALGATES2=7           % STACK
INDEX CALPRODUCTS2=6        % REGISTERS
%
INDEX IMPULSEPRODUCTS=5     % SUBROUTINE IMPULSE APB STACK REGISTERS
INDEX IMPULSEGATES=4        % AND TEMPORARY STORAGES FOR
INDEX LAGMAX=3              % SUBROUTINES REMOTE1 AND REMOTE2.
%                           % 5 IS THE TEMPORARY STORAGE FOR MARGIN AND
%                           % CALPRODUCTS, 4 IS THE TEMPORARY STORAGE FOR
%                           % SIGSAMPLES AND CALGATES.
%
INDEX USERREG=2             % FREE REGISTER FOR USERS PARAMETERS
%
INDEX BUFSTARTADD=1 % APB ADDRESS COUNTER
INDEX APBINCR=0     % APB INCREMENT (=1)



INDEX RESENTRY1=15,RESENTRY2=14,RESENTRY3=13,RESENTRY4=12
INDEX RESENTRY5=11,RESENTRY6=10,RESENTRY7=9,RESENTRY8=8
INDEX RESENTRY9=7,RESENTRY10=6
%
INDEX RESJUMPER=5     % TEMPORARY STORAGE FOR MAXLAG+1
%
INDEX MAXLAG=4        % SUBROUTINE IMPULSE REGISTER AND THE TEMPORARY
%                     % STORAGE FOR MAXLAG1 AND MAXLAG2.
%
INDEX MAXLAG1=3       % REMOTE1 APM STACK REGISTER
%
INDEX MAXLAG2=2       % REMOTE2 APM STACK REGISTER
%
INDEX RESTARTADD=1    % APM ADDRESS COUNTER
INDEX APMINCR=0       % APM INCREMENT  (=1)
%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%          THE FOLLOWING THREE SUBROUTINE CALLS ARE ALLOWED:
%
%          NEXT
%          RSAPM(RESTARTADD)=RSAPM(RESENTRYN)
%          RELOAD LCR2
%          RELOADVALUE=...(ANY SUITABLE REGISTER COMBINATION)
%          CALL REMOTE1
%
%          NEXT
%          RSAPM(RESTARTADD)=RSAPM(RESENTRYN)
%          RELOAD LCR2
%          RELOADVALUE=...(ANY SUITABLE REGISTER COMBINATION)
%          CALL REMOTE2
%
%          NEXT
%          RSAPM(RESTARTADD)=RSAPM(RESENTRYN)
%          RSAPB(IMPULSEGATES)=...(ANY SUITABLE REGISTER COMBINATION)
%          CALL IMPULSE
%
%          THE THIRD LINE IS NOT ALWAYS NEEDED IN CALL IMPULSE CALL,
%          SEE ALSO THE NOTES BELOW.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%          THE MAXIMUM NUMBER OF USER DEFINED PROGRAM STEPS IS 15
%
%          NOTE: IN THE EXAMPLE COMMANDS THE RESENTRYN MUST BE SOME
%                OF THE POSSIBILITIES RESENTRY1,RESENTRY2...RESENTRY10.
%
%                THE RSAPM(RESTARTADD)=.... CAN ALSO BE IN THE FORM:
%                RSAPM(RESTARTADD)=0
%
%                THE RSAPM(RESTARTADD)=.. SPECIFIES THE FIRST RESULT
%                MEMORY ADDRESS  USED BY THE CALLED SUBROUTINE. THIS
%                ADDRESS IS GIVEN BY THE USER IN THE CORRELATOR APM
%                STACK LOADING. REGISTERS APM(15)-APM(5) HAVE BEEN
%                RESERVED FOR RESENTRIES RESENTRY1....RESENTRY11.
%
%                IF THE LINE RSAPM(RESTARTADD)=... IS OMITTED ,THE
%                DEFAULT VALUE IS THE FIRST HIGHER ADDRESS WHICH WAS NOT
%                USED BY THE PREVIOUS SUBROUTINE AND IN CASE OF THE FIRST
%                SUBROUTINE CALL THE DEFAULT VALUE IS  ZERO.
%
%          NOTE: "...(ANY SUITABLE REGISTER COMBINATION)" IS OFTEN SOME
%                OF THE FOLLOWING:
%
%                0
%                RSAPB(APBINCR)                  %=1
%                RSAPB(APBINCR)+RSAPB(APB(INCR)  %=2
%                RSAPB(USERREG)
%
%
%          NOTE: IN CONNECTION WITH "CALL IMPULSE" THE APB REGISTER
%                HAVING LOADING IN THE SUBROUTINE CALL CAN ALSO BE THE
%                IMPULSEPRODUCTS BUT NOT LAGMAX WITHOUT CORRESPONDING
%                APM REGISTER LOADING (MAXLAG) WHICH IS POSSIBLE ONLY IF
%                THERE IS NO NEED TO LOAD RESTARTADD.
%
%          NOTE: THE LAST SUBROUTINE MUST BE THE ONE , WHICH
%                USES THE HIGHEST ADDRESSES IN THE RESULT MEMORY.
%                THE SCANCOUNT IS WRITTEN TO THE NEXT UNUSED
%                RESULT MEMORY POSITION. IF THIS RULE IS VIOLATED,
%                ONE HAS TO LOAD RSAPM(RESTARTADD) TO SCANCOUNTADDRESS
%                AS A LAST COMMAND OF THE MAIN PROGRAM. ONE POSSIBLE FORM
%                TO DO THIS IS THE FOLLOWING COMMAND:
%
%                  NEXT
%                  RSAPM(RESTARTADD)=RSAPM(RESENTRYN)
%                  GOTO SCANCOUNT
%                  %RESENTRYN CONTAINS THE SCANCOUNTADDRESS (DATAIO-1)
%
%          NOTE: THE SUBROUTINES ARE WRITTEN SO THAT THE
%                FIRST SAMPLE TAKEN FROM THE BUFFER MEMORY
%                IS IMMEDIATELY AFTER THE LAST USED SAMPLE
%                OF THE PREVIOUS SUBROUTINE. THE FIRST
%                SUBROUTINE ASSUMES BUFFER MEMORY START
%                ADDRESS TO BE ZERO.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%           RULES FOR THE SUBROUTINES REMOTE1 AND REMOTE2
%       *****************************************************
%
%
%      1) ***LCR2 IS LOADED TO 1 (APBINCR) IN THE SUBROUTINE CALL***
%
%      LCR2 LOADING GIVES THE NUMBER OF GATES FOR THE SCATTER ACF
%      COMPUTATION (TRIANGULAR WEIGHTED ACF WITH PROGRAMMABLE LAGMAX).
%
%      IF MARGIN=0 THEN POWERPROFILE (TIMINGCKECK) IS NOT COMPUTED
%      AND THE COMPUTATION STARTS FROM THE SCATTERACF.
%
%      IF ALSO SIGSAMPLES=0 THEN  THE COMPUTATION STARTS FROM THE
%      BOXCAR WEIGHTED (IMPULSE COMPUTATION) ACF WITH THE LAGMAX, CALGATES
%      AND CALPRODUCTS GIVEN IN THE APB STACK FOR THE CALLED SUBROUTINE.
%      THIS IS ONE OF THE POSSIBILITIES TO COMPUTE "IMPULSE".
%
%      IF EITHER CALPRODUCTS OR CALGATES IS ZERO THEN THE COMPUTATION
%      TERMINATES AFTER SCATTER ACF.
%
%      2) ***LCR2 IS LOADED TO ZERO IN THE SUBROUTINE CALL***
%
%      IF THE NUMBER OF GATES GIVEN FOR THE SCATTER ACF (LCR2 LOADING
%      IN THE SUBROUTINE CALL) IS ZERO,  THE COMPUTATION
%      STARTS FROM THE BOXCAR WEIGHTED CALIBRATION ACF (IMPULSE).
%
%
%      3) *** LCR2 LOADED TO A VALUE GREATER THAN 1***
%
%      IF THE NUMBER OF GATES GIVEN FOR THE SCATTER ACF IS
%      NEITHER 1 NOR ZERO THEN THE POWERPROFILE TIMING CHECK
%      IS SKIPPED AND THE SCATTER GATES ARE COMPUTED WITH SAMPLE SPACE
%      OVERLAPPING = - MARGIN, (MARGIN LOADED AS A NEGATIVE NUMBER IN
%      THIS CASE). AFTER SCATTER GATES PROGRAM BRANCHES TO BOXCAR ACF
%      IF THAT PART IS NOT SKIPPED.
%
%      THIS GIVES POSSIBILITY TO MAKE TIMING CHECK USING ACF:S INSTEAD
%      OF POWERPROFILES. SIMULTANEOUS USE OF BOTH POWERPROFILE AND ACF TYPES
%      OF TIMING (AND POWER STABILITY) CHECKING DEMANDS THREE STEP
%      SUBROUTINE CALLS AND IS NOT EXPLAINED HERE.
%
%      WITH THE EXPLAINED WAY ONE CAN ALSO SIMULATE MANY OLDER CORRELATOR
%      PROGRAMS (PROGRAMMABLE NUMBER OF LAGS AND OVERLAPPING)
%            -----------------------------------------
%
%
%               RULES FOR THE SUBROUTINE IMPULSE:
%          ********************************************
%
%      1) *** IMPULSE COMPUTATION USING REMOTE1 AND/OR REMOTE2***
%
%      ONE WAY TO GET THE "CALL IMPULSE" COMPUTATION IS TO SKIP THE TIMING
%      CHECK AND SCATTER ACF IN THE REMOTE1 AND/OR REMOTE2. THEN THE
%      NUMBER OF GATES, CALPRODUCTS, LAGMAX AND MAXLAG ARE THOSE LOADED
%      INTO THE CORRESPONDING REGISTERS OF REMOTE1 AND/OR REMOTE2.
%
%      2) *** CALL IMPULSE WHEN NEITHER REMOTE1 NOR REMOTE2 ARE USED***
%
%      ONE OF THE APB REGISTERS CAN BE RELOADED (E.G. "IMPULSEGATES")
%      TO A WANTED VALUE BUT THIS IS NOT NEEDED IF THE REGISTER
%      IS ALREADY CONSTANTLY LOADED TO A WANTED VALUE IN THE ELAN FILE
%      OR HAS THE PROPER VALUE BECAUSE OF PREVIOUS COMPUTATION.
%      THE NUMBER OF GATES IS IN THE "IMPULSEGATES", THE NUMBER OF
%      CROSSPRODUCTS IN "IMPULSEPRODUCTS", THE PARAMETER "LAGMAX"
%      IN THE REGISTER "LAGMAX", AND "MAXLAG" IN THE REGISTER "MAXLAG".
%
%      IF THE SUBROUTINE IMPULSE IS THE ONLY SUBROUTINE USED IN THE
%      PROGRAM, THEN THE REGISTERS MENTIONED ABOVE HAVE TO BE LOADED
%      TO WANTED THE VALUES IN THE ELAN FILE. NOTE THE POSSIBILITY TO
%      RELOAD ONE OF THE APB REGISTERS IN THE SUBROUTINE CALL.
%
%      3)  ***CALL IMPULSE WHEN REMOTE1 AND/OR REMOTE2 ARE ALSO USED***
%
%      THE REGISTERS CONTROLLING THE COMPUTATION HAVE A VALUE
%      LOADED TO THESE REGISTERS BY THE PREVIOUSLY CALLED SUBROUTINE
%      (REMOTE1 OR REMOTE2). IMPULSEPRODUCTS IS CALPRODUCTS1 OR CALPRODUCTS2,
%      LAGMAX IS LAGMAX1 OR LAGMAX2, AND MAXLAG IS MAXLAG1 OR MAXLAG2 ETC.
%      DEPENDING ON WHICH ONE OF THE ROUTINES REMOTE1 OR REMOTE2 WAS
%      CALLED IN THE PREVIOUS MAIN PROGRAM STEP. ONE OF THE APB REGISTERS
%      CAN BE RELOADED IN THE SUBROUTINE CALL (OFTEN NUMBER OF GATES) AND
%      RESULT MEMORY START ADDRESS PROGRAMMED.
%
%      IF THE "CALL IMPULSE" IS THE FIRST SUBROUTINE CALL IN THE MAIN
%      PROGRAM, THEN THE STARTING VALUES MUST BE LOADED IN THE ELAN FILE
%      TO PREVENT POSSIBLE CORRELATOR "HANGING".
%
%      SUBROUTINE IMPULSE DOES NOT CHANGE THE CONTENTS OF THE CONTROL
%      REGISTERS IT USES.
%                    ------------------------------
%
%
%
%             POWER PROFILE COMPUTATION USING GEN-REMOTE
%          ************************************************
%
%
%      THE POWER PROFILE TYPE OF TIMING CHECKING IS ARRANGED SO THAT
%      IT IS NOT VERY SUITABLE FOR INDEPENDENT POWER PROFILE COMPUTATION.
%      ON THE DIFFERENT POSSIBILITIES THE FOLLOWING IS RECOMMENDED.
%
%      LOAD CALPRODUCTS1 TO NUMBER OF SAMPLES ADDED TOGETHER AND CALGATES1
%      TO NUMBER OF GATES. LOAD LAGMAX1 AND MAXLAG1 REGISTERS TO ZERO.
%      CALL REMOTE1  WITH LCR2 LOADING=0. THIS PRODUCES GATED POWERPROFILE
%      IF CALPRODUCTS1#0 AND NORMAL POWERPROFILE IF CALPRODUCTS1=0.
%      SIMILAR COMPUTATION IS POSSIBLE WITH REMOTE2 AND ALSO WITH IMPULSE.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%   THE FIRST COMMAND IS THE IDLE LOOP   %%%%%%%%%%%%%%%%%%%%%%%
%%%%   DO NOT TOUCH THIS COMMAND            %%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
LOCATION=0
LABEL ZERO
RSAPB(BUFSTARTADD)=0
RSAPM(RESTARTADD)=0
CONTINUE                  %THE IDLE LOOP ENDS HERE

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%   THE MAIN PROGRAM WRITTEN BY THE USER BEGINS HERE  %%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%        LABEL FOR GET-STRING : MAINPROG              %%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%          GEN-5-R-SUBR:LIST
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
NEXT
CALL SUBRLIST

NEXT
CALL SUBRLIST

NEXT
RSAPM(RESTARTADD)=RSAPM(RESENTRY3)
GOTO SCANCOUNT

NEXT
SUBROUTINE SUBRLIST
RSAPM(RESTARTADD)=RSAPM(RESENTRY1)
RELOAD LCR2
RELOADVALUE=RSAPB(USERREG)
CALL REMOTE1                          %5*SIGNAL +B+B+NI

NEXT
RSAPM(RESTARTADD)=RSAPM(RESENTRY2)    %B+B+NI
CALL REMOTE2                          %POSITION ONCE USED

NEXT
RSAPM(RESTARTADD)=RSAPM(RESENTRY2)    %B+B+NI
CALL REMOTE2

NEXT
RSAPM(RESTARTADD)=RSAPM(RESENTRY1)   %5*SIGNAL+B+B+NI
RELOAD LCR2
RELOADVALUE=RSAPB(USERREG)
CALL REMOTE1

NEXT
RETURN


%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%   THE USER DEFINED MAIN PROGRAM ENDS HERE   %%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%   POSSIBLE USER'S SUBROUTINES CAN  BE APPENDED  %%%%%%%%
%%%%%   AFTER THE SCANCOUNT COMMAND  GIVEN BELOW      %%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                            %
%    SCANCOUNT TERMINATES THE COMPUTATION AND                %
%    MUST BE THE FIRST COMMAND AFTER THE USER'S              %
%    MAIN PROGRAM                                            %
%                                                            %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
NEXT
LABEL SCANCOUNT
RELOAD LCR2
RELOADVALUE=RSAPB(APBINCR)      %NEEDED IN TRANSFER ROUTINE
RESMEMADDRESS=RSAPM(RESTARTADD)
CHANNEL1=-1 CHANNEL2=-1
INITIALIZE ACCUMULATOR
LOAD IREG
STORE OREG
STROBE IREG
GOTO LASTCOMMAND
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                                  %
%    POSSIBLE USER'S SUBROUTINES CAN  START HERE. THE MOST COMMON  %
%    FORM IS A SUBROUTINE CONTAINING A LIST OF SUBROUTINE CALLS    %
%                                                                  %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                           %
%     EVERYTHING WRITTEN BY THE USER MUST END HERE          %
%                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                           %
%     SUBROUTINE REMOTE1                                    %
%                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
NEXT
SUBROUTINE REMOTE1
RSAPB(IMPULSEPRODUCTS)=RSAPB(MARGIN1)
RSAPM(RESJUMPER)=RSAPM(MAXLAG1)
CONTINUE

NEXT
RSAPB(LAGMAX)=RSAPB(LAGMAX1)
RSAPM(MAXLAG)=RSAPM(MAXLAG1)
LC2=LCR2
CONTINUE

NEXT
RSAPB(IMPULSEGATES)=RSAPB(SIGSAMPLES1)
RSAPM(RESJUMPER)=RSAPM(RESJUMPER)+RSAPM(APMINCR)
CALL SCATTERCOMPUTE

NEXT
RSAPB(IMPULSEPRODUCTS)=RSAPB(CALPRODUCTS1)
CONTINUE

NEXT
RSAPB(IMPULSEGATES)=RSAPB(CALGATES1)
GOTO BOXCARACF
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                            %
%     SUBROUTINE REMOTE2                                     %
%                                                            %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
NEXT
SUBROUTINE REMOTE2
RSAPB(IMPULSEPRODUCTS)=RSAPB(MARGIN2)
RSAPM(RESJUMPER)=RSAPM(MAXLAG2)
GOTO RESTOFREMOTE2

%  CONTINUES AFTER THE TRANSFER ROUTINE

%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                            %
%       SUBROUTINE SCATTERCOMPUTE                            %
%                                                            %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
NEXT
SUBROUTINE SCATTERCOMPUTE
RELOAD LCR3
RELOADVALUE=RSAPB(IMPULSEGATES)               %=SIGSAMPLES
IF (LC2=0) THEN RETURN ELSE CALL DUMMY        %RETURN IF SCATTERGATES=0

NEXT
LC2=LC2-1                                     %TO CHECK IF LCR2=1
LC3=LCR3
RELOAD LCR1
RELOADVALUE=RSAPB(IMPULSEPRODUCTS)            %=MARGIN
CONTINUE

NEXT
QAPB=RSAPB(IMPULSEPRODUCTS)+RSAPB(IMPULSEPRODUCTS) %=2*MARGIN
IF (LC3=0) THEN RETURN ELSEIF (LC2#0) THEN GOTO SIGNALGATE
OTHERWISE CONTINUE

% IF SIGSAMPLES =0 THEN RETURN ELSEIF NUMBER OF SIGNALGATES
% NOT EQUAL 1 GOTO SIGNALGATE OTHERWISE CONTINUE

NEXT
RELOAD LCR3
RELOADVALUE= QAPB+RSAPB(IMPULSEGATES)   %=2*MARGIN+SIGSAMPLES
LC1=LCR1

NEXT
QAPM=RSAPM(RESTARTADD)-RSAPM(APMINCR)
QAPB=RSAPB(BUFSTARTADD)-RSAPB(APBINCR)
LC3=LCR3
IF (LC1=0) THEN GOTO SIGNALGATE ELSE CONTINUE %SKIP TIMING IF MARGIN=0

NEXT
LC3=LC3-1
IF (LC3=0) THEN RETURN ELSE CONTINUE   %RETURN IF 2*MARGIN+SIGSAMPLES=0

NEXT
LABEL ENTERPOWER1
BUFFERADDRESS=QAPB+RSAPB(APBINCR)
QAPB=BUFFERADDRESS
RESMEMADDRESS=QAPM+RSAPM(APMINCR)
QAPM=RESMEMADDRESS
CHANNEL1=X*X+Y*Y
CHANNEL2=Y*X-X*Y
INITIALIZE ACCUMULATOR
STROBE IREG
LOAD IREG
STORE OREG
LC3=LC3-1
IF (LC3=0) THEN CONTINUE ELSE GOTO ENTERPOWER1

NEXT
RSAPM(RESTARTADD)=QAPM+RSAPM(APMINCR)
RSAPB(BUFSTARTADD)=RSAPB(BUFSTARTADD)+RSAPB(IMPULSEPRODUCTS)
GOTO SIGNALGATE
%                  %IMPULSEPRODUCTS = MARGIN

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                           %
%      SUBROUTINE DUMMY  (MODIFIED)                         %
%                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
NEXT
SUBROUTINE DUMMY
QAPB=RSAPB(BUFSTARTADD)-RSAPB(APBINCR)
RETURN
%

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                           %
%   PROGRAM GEN-SHORT-DUMP  (8 STEPS LONG)                  %
%                                                           %
%   THIS PROGRAM CONTROLS THE DATA TRANSFER   AND           %
%   MUST START FROM THE LOCATION 32                         %
%   IN THIS PROGRAM VERSION  EXTRA TIME IS GIVEN FOR DATA   %
%   BUS SETTLING BEFORE CAMAC BUS MULTIPLEXER               %
%                                                           %
% THIS PROGRAM VERSION DEMANDS LCR2 LOADING TO APBINCR  IN  %
% THE COMMAND SEQUENCE: SCANCOUNT-LASTCOMMAND-IDLE          %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
LOCATION=32
LABEL DUMP
PREPARETRANSFER
RELOAD LCR1
RELOADVALUE=DATAI
LC2=LC2-1
IF (LC2=0) THEN CONTINUE ELSE GOTO DUMP
%
% DON'T WORRY ABOUT THE FACT THAT THE CORRELATOR PROGRAMMING RULES HAVE
% BEEN VIOLATED IN THE PREVIOUS COMMAND!!!! (RELOAD OPERATION IN A LOOP)
%
NEXT
TRANSFER STATUSWORD
QAPM=-RSAPM(APMINCR)
CONTINUE

NEXT
TRANSFER CONTROLWORD
LC1=LCR1
CONTINUE

NEXT
LABEL TRANSFERLOOP
LC1=LC1-1
RESMEMADDRESS=QAPM+RSAPM(APMINCR)
QAPM=RESMEMADDRESS
QAPB=RSAPB(APBINCR)+RSAPB(APBINCR)
TRANSFER CHANNEL1 MSPART
CONTINUE

NEXT
RESMEMADDRESS=QAPM
TRANSFER CHANNEL1 LSPART
RELOAD LCR2
RELOADVALUE=QAPB+RSAPB(APBINCR)
CONTINUE

NEXT
RESMEMADDRESS=QAPM
TRANSFER CHANNEL2 MSPART
CONTINUE

NEXT
RESMEMADDRESS=QAPM
TRANSFER CHANNEL2 LSPART
LC2=LCR2
IF (LC1=0) THEN CONTINUEANDPUSH ELSE GOTO TRANSFERLOOP

NEXT
LABEL FINISHLOOP
LC2=LC2-1
FINISHTRANSFER
SET START-EXPERIMENT MODE
IF (LC2=0) THEN GOTO ZERO ELSE GOBACK
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%   THE LAST FOUR COMMANDS OF REMOTE2
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

NEXT
LABEL RESTOFREMOTE2
RSAPB(LAGMAX)=RSAPB(LAGMAX2)
RSAPM(MAXLAG)=RSAPM(MAXLAG2)
LC2=LCR2
CONTINUE

NEXT
RSAPB(IMPULSEGATES)=RSAPB(SIGSAMPLES2)
RSAPM(RESJUMPER)=RSAPM(RESJUMPER)+RSAPM(APMINCR)
CALL SCATTERCOMPUTE

NEXT
RSAPB(IMPULSEPRODUCTS)=RSAPB(CALPRODUCTS2)
CONTINUE

NEXT
RSAPB(IMPULSEGATES)=RSAPB(CALGATES2)
GOTO BOXCARACF

%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                            %
%    PROGRAM BLOCK SIGNALGATE                                %
%                                                            %
%    LC1=LAGCOUNTER                                          %
%    LC2=GATE COUNTER                                        %
%    LC3=SAMPLECOUNTER   (COUNTS SIGSAMPLES - LAGMAX)        %
%                                                            %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
NEXT
LABEL SIGNALGATE
RELOAD LCR1
RELOADVALUE=RSAPB(LAGMAX)
CALL DUMMY

NEXT
LC1=LCR1
RELOAD LCR3
RELOADVALUE=RSAPB(IMPULSEGATES)-RSAPB(LAGMAX)
CALL DUMMY        % IMPULSEGATES = SIGSAMPLES

NEXT
RSAPB(IMPULSEPRODUCTS)=RSAPB(IMPULSEPRODUCTS)+RSAPB(APBINCR)
LCR1A=LC1         % IMPULSEPRODUCTS = MARGIN+1 FOR COMING USE
LC3=LCR3
LC2=LCR2
INITIALIZE ACCUMULATOR
IF (LC2=0) THEN GOTO LAGDECREASE  ELSE CONTINUEANDPUSH

NEXT
RSAPB(BUFSTARTADD)=QAPB+RSAPB(APBINCR)
IF (LC2=0) THEN RETURN ELSE GOTO LAGDECREASE
%
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                            %
%     BROGRAM BLOCK SCATTERACF                               %
%                                                            %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
NEXT
LABEL SCATTERACF
BUFFERADDRESS=RSAPB(BUFSTARTADD)
QAPB=BUFFERADDRESS
RESMEMADDRESS=RSAPM(RESTARTADD)
QAPM=RESMEMADDRESS
CHANNEL1=X*X+Y*Y
CHANNEL2=Y*X-X*Y
STROBE IREG
LOAD IREG
STORE OREG
IF (LC1=0) THEN LC1=LCR1  LC2=LC2-1 ELSE LC1=LC1-1
IF (LC1=0) THEN GOTO LASTLONG ELSE CONTINUE

NEXT
LABEL SIGLOOP
BUFFERADDRESS=QAPB+RSAPB(APBINCR)
QAPB=BUFFERADDRESS
RESMEMADDRESS=QAPM+RSAPM(APMINCR)
QAPM=RESMEMADDRESS
CHANNEL1=OLDVALUE*X+OLDVALUE*Y
CHANNEL2=OLDVALUE*X-OLDVALUE*Y
STROBE IREG
LOAD IREG
STORE OREG
IF (LC1=0) THEN LC1=LCR1A ELSE LC1=LC1-1
IF (LC1=0) THEN CONTINUE ELSE GOTO SIGLOOP

NEXT
LC1=LC1-1
RSAPB(BUFSTARTADD)=RSAPB(BUFSTARTADD)+RSAPB(APBINCR)
ACCUMULATE
IF (LC3=0) THEN CONTINUE ELSE GOTO LAGDECREASE

NEXT
LCR1A=LC1
GOTO SCATTERACF

NEXT
LABEL LAGDECREASE
LC1=LCR1A
LC3=LC3-1
GOTO SCATTERACF

NEXT
LABEL LASTLONG
RSAPB(BUFSTARTADD)=RSAPB(BUFSTARTADD)+RSAPB(IMPULSEPRODUCTS)
RSAPM(RESTARTADD)=RSAPM(RESTARTADD)+RSAPM(RESJUMPER)
LCR1A=LC1                         %IMPULSEPRODUCTS = MARGIN+1
LC3=LCR3                          %RESJUMPER=MAXLAG+1
INITIALIZE ACCUMULATOR
IF (LC2=0) THEN RETURN ELSE GOTO LAGDECREASE

%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                           %
%     SUBROUTINE IMPULSE                                    %
%                                                           %
%     PROGRAM BLOCK BOXCARACF                               %
%                                                           %
%     LC1=PRODUCT COUNTER                                   %
%     LC2=LAG COUNTER                                       %
%     LC3=GATE COUNTER                                      %
%                                                           %
%                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
NEXT
SUBROUTINE IMPULSE
LABEL BOXCARACF
RELOAD LCR1
RELOADVALUE=RSAPB(IMPULSEPRODUCTS)
CALL DUMMY

NEXT
LC1=LCR1
RELOAD LCR3
RELOADVALUE=RSAPB(IMPULSEGATES)
CALL DUMMY

NEXT
LC3=LCR3
RELOAD LCR2
RELOADVALUE=RSAPB(LAGMAX)
LC1=LC1-1
IF (LC1=0) THEN RETURN ELSE CALL DUMMY

NEXT
LC2=LCR2
LCR1A=LC1
LC3=LC3-1
INITIALIZE ACCUMULATOR
IF (LC3=0) THEN RETURN ELSE CONTINUE

NEXT
LABEL CALCOMPUTE
BUFFERADDRESS=RSAPB(BUFSTARTADD)
QAPB=BUFFERADDRESS
RESMEMADDRESS=RSAPM(RESTARTADD)
QAPM=RESMEMADDRESS
CHANNEL1=X*X+Y*Y
CHANNEL2=Y*X-X*Y
STROBE IREG
LOAD IREG
STORE OREG
LC2=LC2-1
IF (LC2=0) THEN GOTO PRODUCTTEST ELSE CONTINUEANDPUSH

NEXT
LABEL LAGLOOP
BUFFERADDRESS=QAPB+RSAPB(APBINCR)
QAPB=BUFFERADDRESS
RESMEMADDRESS=QAPM+RSAPM(APMINCR)
QAPM=RESMEMADDRESS
CHANNEL1=OLDVALUE*X+OLDVALUE*Y
CHANNEL2=OLDVALUE*X-OLDVALUE*Y
STROBE IREG
LOAD IREG
STORE OREG
LC2=LC2-1
IF (LC2=0) THEN CONTINUEANDPOP ELSE GOBACK

NEXT
LABEL PRODUCTTEST
RSAPB(BUFSTARTADD)=RSAPB(BUFSTARTADD)+RSAPB(APBINCR)
LC2=LCR2
ACCUMULATE
IF (LC1=0) THEN LC1=LCR1A ELSE LC1=LC1-1
IF (LC1=0) THEN CONTINUE ELSE GOTO CALCOMPUTE

NEXT
LABEL FINISHCALGATE
RSAPB(BUFSTARTADD)=RSAPB(BUFSTARTADD)+RSAPB(LAGMAX)
RSAPM(RESTARTADD)=QAPM+RSAPM(APMINCR)
INITIALIZE ACCUMULATOR
LC3=LC3-1
IF (LC3=0) THEN RETURN ELSE GOTO CALCOMPUTE

%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                           %
% THE LAST COMMAND TERMINATES THE COMPUTATION AND           %
% THE CORRELATOR GOES TO THE IDLE LOOP                      %
%                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
NEXT
LABEL LASTCOMMAND
STROBE IREG
LC2=LCR2                     %FOR TRANSFER ROUTINE
SET CONTINUE-EXPERIMENT MODE
GOTO ZERO
%
END

                                       %
%                                                           %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
NEXT
SUBROUTINE IMPULSE
LABEL BOXCARACF
RELOAD LCR1
RELOADVALUE=RSAPB(IMPULSEPRODUCTS)
CALL DUMMY

NEXT
LC1=LCR1
RELOAD LCR3
RELOADVALUE=RSAPB(IMPULSEGATES)
CALL DUMMY

NEXT
LC3=LCR3
RELOAD LCR2
RELOADVALUE=RSAPB(LAGMAX)
LC1=LC1-1
IF (LC1=0) THEN RETURN ELSE CALL DUMMY

NEXT
LC2=LCR2
LCR1A=LC1
LC3=LC3-1
INITIALIZE ACCUMULATOR
IF (LC3=0) THEN RETURN ELSE CONTINUE

NE
