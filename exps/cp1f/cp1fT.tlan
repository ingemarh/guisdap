%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  ***  T A R L A N   P R O G R A M   F O R   T R O M S O   ***
%%
%%  EXPERIMENT: COMMON PROGRAM ONE               VERSION   F
%%
%%  USING CORRELATOR PROGRAM BY REGIS GRAS FOR SPECIAL DOUBLE
%%  MULTIPULSE, SINGLE PULSE AND POWER PROFILES.
%%
%%    1 4*20 US MULTIPULSE SEQUENCE FOR E REGION
%%    1 3*20 US MULTIPULSE SEQUENCE FOR E REGION
%%    1 20 US POWER PROFILE FOR D AND E REGION
%%    1 60 US POWER PROFILE FOR E AND LOWER F REGION
%%    2 360 US PULSES, ONE FOR F REGION ACFS FOR TROMSO,
%%                     BOTH USED FOR REMOTE SITE ACFS
%%
%%  TYPE: COMMON PROGRAM.
%%
%%  FILE NAME: (EXP)CP-1-F-T:TLAN
%%
%%
%%  22 SEPTEMBER 1984    SHEILA KIRKWOOD, TROMSO
%%          LAST ALTERED 14 NOVEMBER 1984
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% FIRST THE TRANSMITTING SEQUENCE
%
AT 1 TRANS,CHQPULS
AT 10 RXPROT,LOPROT
AT 40 BEAMON
AT 60 F0                     % FREQUENCY SET AT F0 FOR REMOTE SITE PULSE
AT 100 RFON
SETTCR 360                   % SETTCR TO THE REQUIRED PULSE LENGTH
AT 100 F5                    % FREQUENCY SET AT F5  FOR SINGLE PULSE (CH7)
AT 460 RFOFF,F3              % FREQUENCY SET AT F3 FOR FIRST MULTIPULSE (CH3)
AT 462 RFON
AT 482 RFOFF
AT 502 RFON
AT 522 RFOFF
AT 530 F1                    % FREQUENCY SET AT F1 FOR SECOND MULTIPULSE (CH4)
AT 544 RFON
AT 564 RFOFF
AT 604 RFON
AT 624 RFOFF
AT 630 F4                    % FREQUENCY SET AT F4  FOR SECOND POWER PROF (CH6)
AT 640 RFON
AT 700 RFOFF,F3
AT 702 RFON
AT 722 RFOFF,F1
AT 724 RFON
AT 744 RFOFF,F2
AT 746 RFON
AT 766 RFOFF
AT 780 F3
AT 782 RFON
AT 802 RFOFF,BEAMOFF
AT  900 RXPOFF
AT 1053 LOPOFF
%
% NEXT THE RECEIVING SEQUENCE
%
AT 1054 RECEV
AT 1057 CH1
AT 1139 CH2
AT 1141 CH3
AT 1177 CH5
AT 1285 CH4
AT 1936 CH3OFF               % 80 SAMPLES WITH 10US SAMPLING
AT 1954 CH2OFF               % 41 SAMPLES WITH 20US SAMPLING
AT 1972 CH1OFF               % 46 SAMPLES WITH 20US SAMPLING
AT 3020 CH4OFF               % 174 SAMPLES WITH 10US SAMPLING
AT 5492 CH5OFF               % 432 SAMPLES WITH 10US SAMPLING
%
% AND NOW THE CALIBRATION
%
SETTCR 6560
AT 60 CAL100
AT 100 CH5
AT 460 CH1
AT 540 CH2
AT 640 CH4
AT 740 CH3
AT 815 CH2OFF                % 14 SAMPLES WITH 20US SAMPLING
AT 816 CH4OFF                % 18 SAMPLES WITH 10US SAMPLING
AT 835 CH3OFF                % 10 SAMPLES WITH 10US SAMPLING
AT 836 CH1OFF                % 19 SAMPLES WITH 20US SAMPLING
AT 1175 CH5OFF               % 108 SAMPLES WITH 10US SAMPLING
AT 1200 CAL0
%
% AND LAST OF ALL THE BACKGROUND
%
SETTCR 7760
AT 100 CH5                   % SAME NUMBERS OF SAMPLES AS FOR CALIBRATION
AT 460 CH1
AT 540 CH2
AT 640 CH4
AT 740 CH3
AT 815 CH2OFF
AT 816 CH4OFF
AT 835 CH3OFF
AT 836 CH1OFF
AT 1172 STC
AT 1175 CH5OFF 
AT 1210 BUFLIP
SETTCR 0
AT 9009 REP

