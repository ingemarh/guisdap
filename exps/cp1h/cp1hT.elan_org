%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%   ***  E R O S   P R O G R A M M E   F O R   T R O M S O  ***
?
?    ===============
?    C P - 1 - I - T
?    ===============
?
%     FILE: (CP)CP-1-I-T:ELAN
%
%     LAST ALTERED:  4 SEPT  1989  M. RIETVELD
%
%      WRITTEN BY TAUNO TURUNEN
%      EISCAT HQ , OCTOBER 1986
%
%      A GENERAL PURPOSE PROGRAM FOR AURORAL RESEARCH
%      USING 5-PULSECODES IN 12-BLOCK MODE.
%      DECODING PULSE CODE ROUTINES USED IN THE CORRELATOR.
%
%      MODULATIONS: 2 LONG PULSES OF 350 US
%                   2 LOW RESOLUTION POWER PROFILES OF 30 US
%                   4 HIGH RESOLUTION POWERPROFILE PULSES USED AS
%                     D- AND E- LAYER POWERPROFILES  AND AS ZEROLAGS
%                     IN LOW ALTITUDE ACF DATA, PULSE LENGHT 14 US.
%                   4 FIVE-PULSE CODES, USED FOR LAGS 1-11 IN LOW ALTITUDE
%                     ACF DATA. PULSE LENGHT 14 US. LAG INCREMENT 30 US.
%                     NOTE: LAG 10 IS A MISSING LAG (NO TARGET CONTRIBUTION)
%      BEAM IS FIELD ALLIGNED AT 278.6 KM (IGRF MODEL 1985-1990, NOV 1986)
%
%      DOPPLER CALIBRATION IS INCLUDED AS BLOCK 2
%      BLOCK 3 MAY BE USED TO SWITCH BACK FROM CALIBRATION TO EXPERIMENT
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%
WRITE-EXP-HEAD
WRITE-FILE CP-1-I:DESC
WRITE-FILE CP-1-I-T:ELAN
WRITE-FILE CP-1-I-T:TLAN      % GEN-2F-T:TLAN
WRITE-FILE CP-1-I-T:GDEF
%
SET-PARAMETER R1,278.6        % DEFINE INTERSECTION ALTITUDE IN F-REGION
%
   CHANGE-REF-SITE TROMSO,,,,,
   POINT-REF-H 182.57,77.54,R1   % FIELD ALLIGNED FOR RANGE 285KM (GATE 7)
%
SET-SIG-PATH ALLX
LOAD-CORR CP-1-I-T:CCOD       %A SPECIAL CORRELATOR PROGRAM
SYNC -360                     %MATCHED TO USED ELAN AND TLAN FILES
%
LOAD-RA-CON CP-1-I-T:TCOD     %GEN-2F-T:TCOD
%
TRACE-OFF
%
TRANSFER-NOISE-CONT RADAR
%
SET-LO1 1053.5
%
SET-LO2 1,93.5            % F1
SET-LO2 2,92.5            % F3
SET-LO2 3,94.0            % F0
SET-LO2 4,93.0            % F2 (F1 HAS NMT900 INTERFERENCE IN KIRUNA)
SET-LO2 5,92.0            % F4
SET-LO2 6,91.5            % F5
SET-LO2 7,91.0            % F6
SET-LO2 8,89.5            % F9 (CHANGED FROM F7 FOR HRP REASONS MTR 880905)
%
SET-SIG-ATT X,18          % CHANGED FROM 14 890328
%
SET-CH-ATT 1,5
SET-CH-ATT 2,6
SET-CH-ATT 3,5             % INREASED BY 1 DB 890803 MTR
SET-CH-ATT 4,3             % INREASED BY 1 DB 890425 MTR
SET-CH-ATT 5,2
SET-CH-ATT 6,2
SET-CH-ATT 7,3              %BALANCED VALUES FROM DATE 23.6.1986
SET-CH-ATT 8,2              % -MUST BE RECHECKED NOW AND THEN
%
SYNC 10
%
SET-FILTER 1,BU,25.0        %FILTERS IN CH1-CH2-CH3-CH4 MAY BE BU,25.0
SET-FILTER 2,BU,25.0        %OR LI,35.4, PERHAPS EVEN LI,17.7
SET-FILTER 3,BU,25.0
SET-FILTER 4,BU,25.0
SET-FILTER 5,LI,35.4        %FILTERS IN CH5,CH6,CH7,CH8 COULD ALSO BE LI,50.0
SET-FILTER 6,LI,35.4        %NOTE THAT THE DATA IS SAMPLED AT 6 US INTERVALS
SET-FILTER 7,LI,35.4        %LOW BANDWIDTH GIVES BETTER S/N
SET-FILTER 8,LI,35.4
%
%                            %NOTE THAT FILTER CHANGES CAUSE TINY CHANGES
%                            %ALSO IN THE TLAN FILE (DELAYS)
% ------------------
%
SET-COR-APB 15,60    %NUMBER OF E-LAYER GATES
SET-COR-APB 14,96    %ADDRESS SHIFT FROM THE LAST D-PROFILE TO FIRST ZEROLAG
SET-COR-APB 13,3     %NUMBER OF GATES ADDED TOGETHER
SET-COR-APB 12,5     %SYS1
SET-COR-APB 11,10    %SYS2
SET-COR-APB 10,15    %SYS3
SET-COR-APB  9,25    %SYS4
%
%  8-2 ARE WORKING REGISTERS
%
SET-COR-APB  0,1     %APBINCR
%
%  NOTE THAT THE REGISTERS ARE USED FOR MANY PURPOSES AND IN MOST CASES
%  EVEN A MINOR CHANGE DEMANDS REWRITING OF THE CORRELATOR PROGRAM
%
%                    % 0000=THE FIRST D-PROFILE POINT (15+5+40;B+N+D)
SET-COR-APM 15,60    %THE FIRST E-LAYER ACF CAL-POINT (300+60 ;B+N ;(25+5)*12)
SET-COR-APM 14,420   %THE FIRST E-LAYER ACF DATA GATE (720; DATA; 60*12)
SET-COR-APM 13,1140  %THE FIRST LOW RESOLUTION POWER PROFILE (15+5+81;B+N+D)
%                    % 1241=THE FIRST LONG PULSE ACF POINT ((5+1+21)*26,B+N+D)
SET-COR-APM 12,12    %NUMBER OF LAGS IN E-LAEYR ACF
SET-COR-APM 11,1943  %SCANCOUNT ADDRESS
%
SET-COR-APM 10,1     % SYSTEM NUMBERS FOR THE OUTPUT PROCESSOR
SET-COR-APM  9,2
SET-COR-APM  8,3
SET-COR-APM  7,5
%
% 6-2 ARE WORKING REGISTERS
%
SET-COR-APM  0,1     %APMINCR
%
%
SET-COR-DATAIO 1944
%
%
%  **FOR THE DETAILED STRUCTURE OF THE DATA DUMP, SEE THE CP-1-I:DESC**
%
SYNC 10
%
SET-ADC-INT 1,10             %FPP1
SET-ADC-INT 2,10             %FPP2
SET-ADC-INT 3,10             %LP1
SET-ADC-INT 4,10             %LP2
SET-ADC-INT 5,6              %E1,CODE 3152
SET-ADC-INT 6,6              %E2,CODE 2513
SET-ADC-INT 7,6              %E3,CODE 3152
SET-ADC-INT 8,6              %E4,CODE 2513
%
%
SET-BUF-MEM 1,1578    %FPP1 45+15+243 = 303 POINTS  NOISE INJ.+BACKGR.+DATA
SET-BUF-MEM 2,4214    %FPP2 AS ABOVE
SET-BUF-MEM 3,1881    %LP1 325+65+365 = 755 POINTS  SKY+NOISE INJ.+DATA ;ALL
SET-BUF-MEM 4,4517    %LP2 AS ABOVE;ALL CALIBRATION GATES FULLY INDEPENDENT
SET-BUF-MEM 5,0000    %E1 45+15+24+180+235+85+205 =789 POINTS
SET-BUF-MEM 6,0789    %E2 AS ABOVE;D-CAL+D-PROF+ZEROLAG+CODE+CODE CAL.
SET-BUF-MEM 7,2636    %E3 AS ABOVE
SET-BUF-MEM 8,3425    %E4 AS ABOVE
%                     %THE LAST USED ADDRESS IS 5271
%
%                     %NOTE:THE PULSECODE CHANNELS HAVE BEEN CALIBRATED
%                     %     TWICE, ONCE FOR POWERPROFILE AND ONCE FOR ACFS.
%                     %     IN ACF CALIBRATIONS SEPARATE DATA IS SAMPLED
%                     %     FOR ZEROLAG AND OTHER LAGS IN ORDER TO MAKE
%                     %     THE CORRELATOR PROGRAM EASIER.
%
HOLD 10               % GIVE TIME TO LOAD HARDWARE UP TO HERE
%
TRACE-ON
%
START-CORR
%
ENABLE-DMA
%
SYNC 20
%
START-RA-CON 10,1,10
SYNC 10
?
?   -------------------------------------------------------------------
?   SWITCH TX RF ON, CHECK DATA QUALITY. RECORDING STARTS IN 5 MINUTES.
?
?   TO START THE DISPLAY PROGRAM GIVE THE COMMAND '@RTG' FROM GRAPHICS
?   TERMINAL. FOR DOPPLER CALIBRATION GIVE 'GOTO-BLOCK 2' COMMAND
?   -------------------------------------------------------------------
?
SYNC 300
%
BLOCK 1
DOPPLER-CALIBRATION OFF
START-PROGRAM INTCP,,,    % START INTEGRATION PROGRAM
%
TRACE-OFF
%
    SET-PARAMETER I20,1        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
DO -1
  NEW-FILE
?
? ********************************************************************
? * TO STOP THE EXPERIMENT USE 'LIMIT,,'  NOT 'STOP-EXP,,' (DOPPLER) *
? ********************************************************************
?
  DO 3
    SYNC 60

?   POSITION E1:  90.5 KM
    POINT-REF-HEIGHT 182.57,77.54, 90.5
    SET-PARAMETER I20,2        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 90

?   POSITION E2: 101.1 KM
    POINT-REF-HEIGHT 182.57,77.54,101.1
    SET-PARAMETER I20,3        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 60

?   POSITION E3: 116.9 KM
    POINT-REF-HEIGHT 182.57,77.54,116.9
    SET-PARAMETER I20,4        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 60

?   POSITION F:  278.6 KM
    POINT-REF-HEIGHT 182.57,77.54,R1
    SET-PARAMETER I20,5        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 90

?   POSITION E4:  95.8 KM
    POINT-REF-HEIGHT 182.57,77.54, 95.8
    SET-PARAMETER I20,6        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 90

?   POSITION E5: 109.0 KM
    POINT-REF-HEIGHT 182.57,77.54,109.0
    SET-PARAMETER I20,7        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 60

?   POSITION E6: 124.8 KM
    POINT-REF-HEIGHT 182.57,77.54,124.8
    SET-PARAMETER I20,8        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 60

?   POSITION F:  278.6 KM
    POINT-REF-HEIGHT 182.57,77.54,R1
    SET-PARAMETER I20,1        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 30
  ENDDO
ENDDO
%
BLOCK 2
%
STOP-RECORDING
%
?
? >>>>>>>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<<<<<<
? THE DOPPLER-CALIBRATION WILL START IN 1 MIN, PLEASE MONITOR
?
?  ACTIVATE WINDOWS 13 AND 14 ON RTGRAPH TO SEE SPECTRA
? >>>>>>>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<<<<<<
?
SYNC 15
STOP-PROGRAM INTCP
%
WRITE-FILE (CP-EI)CP-DOPPLER:DESC
SET-SIGNAL-ATTENUATOR  X,16
SET-LO2  1,89.9
SET-LO2  2,89.9
SET-LO2  3,89.9
SET-LO2  4,89.9
SET-LO2  5,89.91                   % MULTIPULSE CHANNELS WITH 11 KHZ
SET-LO2  6,89.91                   % DOPPLER OFFSET TO AVOID ALIASING
SET-LO2  7,89.91
SET-LO2  8,89.91
DOPPLER-CALIBRATION ON
SYNC 45
TRACE-OFF
START-RECORDING
DO 2
?  >>> DOPPLER CALIBRATION ON <<<
?  >> ACTIVATE WINDOWS 13 AND 14 ON RTGRAPH TO SEE SPECTRA <<
? TO STOP THE PROGRAM USE "STOP-EXPERIMENT" COMMAND OR WAIT 10 MINUTES
? TO CONTINUE WITH CP-1 USE "GOTO-BLOCK 3"
?
  SYNC 300
ENDDO
DOPPLER-CALIBRATION OFF
STOP-EXPERIMENT,,,,
%
% STANDARD STOP OF EXPERIMENT AFTER DOPPLER-CALIBRATION
%
BLOCK 3
TRACE-OFF
STOP-RECORDING
DUMP-LOGG-FILE
DOPPLER-CALIBRATION OFF
%
SET-LO2 1,93.5            % F1
SET-LO2 2,92.5            % F3
SET-LO2 3,94.0            % F0
SET-LO2 4,93.0            % F2 (F1 HAS NMT900 INTERFERENCE IN KIRUNA)
SET-LO2 5,92.0            % F4
SET-LO2 6,91.5            % F5
SET-LO2 7,91.0            % F6
SET-LO2 8,89.5            % F9 (CHANGED FROM F7 FOR HRP REASONS MTR 880905)
%
SET-SIG-ATT X,18
%
?  >>> DOPPLER CALIBRATION OFF, PREPARING FOR CP-1 SETTINGS <<
?  TO START CP-1 RECORDING USE "GOTO-BLOCK 1 <HR>,<MIN>,<SEC>"
?
% WAIT HERE FOR EITHER STOP-EXPERIMENT OR GOTO-BLOCK 2 (OR 1)
%
DO -1
?  >>> YOU ARE IN BLOCK 3  <<<
?  >>  GOTO-BLOCK 1  OR STOP-EXPERIMENT  <<<
SYNC 600
ENDDO
%
STOP-EXP,,,,
E-O-E

