%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%   ***  T A R L A N  P R O G R A M M E   F O R   T R O M S O  ***
%
%    COMMON PROGRAM CP-1-I AND CP-2-D
%
%    FILE: (CP)CP-1-I-T:TLAN
%
%    LAST ALTERED: 4 JAN 1989 BY M.RIETVELD
%
%
%   GEN-2F, VERSION JUNE 1986. (MODULATION IDENTICAL WITH D-VERSION)
%   A GENERAL PURPOSE EXPERIMENT USING 5-PULSE CODES
%   AND WRITTEN IN 12-BLOCK MODE
%
%   GEN-2F IS INTENDED FOR APPLICATIONS WHEN HIGH SPATIAL
%   RESOLUTION IS NEEDED. FOR MOST APPLICATIONS THE
%   VERSIONS GEN-2A AND GEN-2B HAS TO BE CONSIDERED.
%   C-VERSION IS QUITE SIMILAR THAN D BUT D-VERSION IS A LITTLE MORE
%   ADVANCED. IN E-VERSION ALSO THE MISSING LAG (LAG 10) AND THE LAST LAG
%   (LAG 11) ARE COMPUTED FROM THE PULSE CODES.  THE F-VERSION IS LIKE THE
%   E-VERSION BUT THE NUMBER OF F-LAYER GATES IS HIGHER AND CERTAIN DATA
%   BLOCKS ARE IN DIFFERENT ORDER.
%
%   ELAN, TLAN AND CLAN FILES ARE "MATCHED". THERE ARE NO EASY WAYS
%   TO CHANGE THE MEASURING ALGORITHM.
%
%     1) THE E-LAYER PART IS SAMPLED AT 6 US INTERVALS AND GATED
%        TO 2.7 KM GATE SEPARATION AND ABOUT 3 KM RESOLUTION, 60 GATES
%
%     2) THE E-LAYER PART IS COMPUTED USING ACF FORMAT INSTEAD OF
%        THE UNIPROG MATRIX FORMAT BOTH FOR THE SCATTER DATA AND
%        CALIBRATIONS. IN E- AND F-VERSION ALL THE LAGS ARE HANDLED.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
AT 1 RECEV
AT 5 CH1,CH2
AT 183 CH5,CH6,CH7,CH8
AT 450 ALLOFF,CAL100     %3*15=45 BACKGROUND POINTS IN E-LAYER CHANNELS
%                        %3*15=45 BACKGROUND POINTS IN FPP-CHANNELS
%
AT 500 CH1,CH2           %NOISE INJECTION
AT 558 CH5,CH6,CH7,CH8
AT 645 ALLOFF,CAL0       %3*5=15 NOISE INJECTION SAMPLES IN E-LAYER CHANNELS
%                        %3*5=15 NOISE INJECTION SAMPLES IN FPP-CHANNELS
SETTCR 650
%
%    PHASE 1
%
AT 1 TRANS, CHQPULS
AT 10 RXPROT, LOPROT
AT 50 BEAMON
AT 80 F4
AT 100 RFON            %PP1, CH5,F4
AT 114 RFOFF,F5
AT 115 RFON            %PP2, CH6,F5
AT 129 RFOFF,F6
AT 130 RFON            %PP3, CH7,F6
AT 144 RFOFF,F9                        % CHANGED FROM F7 FOR HRP REASONS MTR 880905
AT 145 RFON            %PP4, CH8,F9
AT 159 RFOFF,BEAMOFF
AT 420 RXPOFF
AT 480 LOPOFF
%
AT 520 RECEV
%
AT 535 CH3,CH4       %BACKGROUND SAMPLES FOR LP CHANNELS
%
AT 561 CH5                   %THE FIRST EPP-GATE AT 68.4 KM
AT 576 CH6                   %THE FIRST ACF GATE AT 90.0 KM
AT 591 CH7                   %THE FIRST 24 SAMPLES USED ONLY FOR POWERPROFILE
AT 606 CH8
%
%
%
AT 1782 CH5OFF
AT 1797 CH6OFF
AT 1812 CH7OFF
AT 1827 CH8OFF
%
AT 1830 ALLOFF         %45 BACKGROUND SAMPLES IN FPP CHANNELS (15 GATES)
%                      %130 BACKGROUND SAMPLES IN LP CHANNELS FOR 2 GATES
%
%  ***NOTE: LP CALIBRATION GATES COMPUTED SO THAT FULLY INDEPENDENT
%           ESTIMATES ARE OBTAINED***
%
%                      % 204 POWERPROFILE POINTS TAKEN AT EVERY CHANNEL
%                      % FOR 8+60 GATES, RANGE IN D-E PROFILES 68.4-173.7 KM
%                      % AND IN E-LAYER ACF:S 90.0-249.3 KM.
%                      % SAMPLING INTERVAL IS 6 US.
SETTCR 2500
%
%    PHASE 2
%
AT 1 TRANS
AT 10 RXPROT,LOPROT
AT 50 BEAMON
AT 80 F5
AT 100 RFON                  %PC2 (2513),CH6,F5
AT 114 RFOFF,F9
AT 115 RFON                  %PC4 (2513),CH8,F9
AT 129 RFOFF,F4
AT 130 RFON                  %PC1 (3152),CH5,F4
AT 144 RFOFF,F6
AT 145 RFON                  %PC3 (3152),CH7,F6
AT 159 RFOFF,F5
AT 160 RFON
AT 174 RFOFF,F9
AT 175 RFON
AT 189 RFOFF,F4
%
AT 220 RFON
AT 234 RFOFF,F6
AT 235 RFON
AT 249 RFOFF,F4
AT 250 RFON
AT 264 RFOFF,F6
AT 265 RFON
AT 279 RFOFF,F1              % CHANGED FROM F2 TO F1 11.4.88 JM
%
AT 280 RFON                  %THE FIRST LOW RESOLUTION POWER PROFILE
AT 309 RFOFF,F5              %AT F1 FOR CHANNEL 1
%
AT 310 RFON
AT 324 RFOFF,F9
AT 325 RFON
AT 339 RFOFF,F5
AT 340 RFON
AT 354 RFOFF,F9
AT 355 RFON
AT 369 RFOFF,F3
%
AT 370 RFON                  %THE SECOND LOW RESOLUTION POWER PROFILE
AT 399 RFOFF,F4              %AT F3 FOR CHANNEL 2
%
AT 400 RFON
AT 414 RFOFF,F6
AT 415 RFON
AT 429 RFOFF,F5
AT 430 RFON
AT 444 RFOFF,F9
AT 445 RFON
AT 459 RFOFF,F4
AT 460 RFON
AT 474 RFOFF,F6
AT 475 RFON
AT 489 RFOFF,BEAMOFF
AT 640 RXPOFF
AT 690 LOPOFF
%
AT 697 RECEV
%
AT 705 CH6                   %THE FIRST GATE AT 90.0 KM RANGE IN PULSECODES
AT 720 CH8
AT 735 CH5
AT 750 CH7
%
AT 795 CH1                   %LOW RESOLUTION POWER PROFILES
AT 885 CH2                   %THE FIRST GATE AT 75 KM RANGE
%
AT 1405 CH3,CH4              %BACKGROUND SAMPLING FOR LP CHANNELS
%
%
AT 2112 CH6OFF
AT 2127 CH8OFF
AT 2142 CH5OFF
AT 2157 CH7OFF               %235 POINTS CH5,CH6,CH7,CH8  FOR 60 GATES
%                            %RANGE 90.0-249.3 KM, GATE SEPARATION 2.70 KM
%
AT 3220 CH1OFF               %243 SAMPLES FOR LOW RESOLUTION POWER PROFILES
AT 3310 CH2OFF               %PRODUCING 81 GATES, RANGE 75.0-435.0 KM
%
%
AT 3350 ALLOFF,CAL100        %195 SAMPLES IN LP CHANNELS FOR 3 BACKGROUND
%                            %GATES. 2+3=5 BACKGROUND GATES ALLTOGETHER
%
AT 3400 CH3,CH4
AT 4045 ALLOFF,CAL0          %65 NOISE INJECTION POINTS /CH3,CH4 (LP)
%
SETTCR 6600
%
%     PHASE 3
%
AT 1 TRANS
AT 10 RXPROT, LOPROT
AT 50 BEAMON
AT 80 F0
AT 100 RFON                  %LP1, CH3,F0  (350 US)
AT 450 RFOFF,F2              %LP2, CH4,F2  (350 US) CHANGED FROM F1 880411
AT 451 RFON
AT 801 RFOFF,BEAMOFF
AT 895 RXPOFF
AT 945 LOPOFF
%
AT 955 RECEV
%
%
AT 965 CH3
AT 1316 CH4                  % THE FIRST LP GATE CENTERED AT 150.0 KM RANGE
%
AT 4473 CH5,CH6,CH7,CH8      % BACKGROUND SAMPLING FOR E-LAYER CHANNELS
%                            % 205 BACKGROUND SAMPLES FOR 25 ACFS
%                            % (75+75+55)
%
AT 4610 CH3OFF
AT 4961 CH4OFF               % 365 SAMPLES IN LP CHANNELS (CH3,CH4), 21 GATES
%                            % RANGE 150.0-600.0 KM (NOMINAL CENTER POINTS)
%                            % (VOLUMEINDEX=15, LPLAGMAX=25)
%
AT 5700 ALLOFF,CAL100        %BACKGROUND SAMPLING FOR THE PULSECODES ENDS
%
%
%                            % 85 INJECTION SAMPLES FOR E-LAYER CHANNELS,
AT 5800 CH5,CH6,CH7,CH8      % 5 ACF:S (15+15+55)
AT 6300 STC
AT 6307 ALLOFF,CAL0
%
AT 6347 BUFLIP
%
%
SETTCR 0000
%
AT 13000 REP                 %LIMITING FACTOR IS THE COMPUTING TIME

