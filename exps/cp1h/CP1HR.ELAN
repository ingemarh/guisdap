%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%   ***  E R O S   P R O G R A M M E   F O R   R E M O T E S  ***
?
?    ===============
?    C P - 1 - I - R  - - - - - - - - - - - - - - - -  VS 89-09-11
?    ===============
?
%     FILE: (CP)CP-1-I-R:ELAN
%
%     LAST ALTERED: 11 SEPTEMBER 1989  M. RIETVELD
%
%     BASED ON GEN-2F-R  BY T.TURUNEN
%     MODULATIONS: 2X350 US FOR REMOTE SITES  AT 6700, F0, CH3, 350 US
%                                                7051 ,F2, CH4, 350 US
%
%     PROGRAMMED BY TAUNO TURUNEN
%     20 OCTOBER 1986,EISCAT HQ
%
%     NOTE! THE DISPLAY CONTROL INFORMATION IS CONTAINED IN THE FILE
%     (CP)CP-1-I-R:GDEF, DOPPLER-CALIBRATION IS INCLUDED AS BLOCK 2
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
TRACE-OFF
WRITE-EXP-HEAD

WRITE-FILE CP-1-I:DESC
WRITE-FILE CP-1-I-R:ELAN
WRITE-FILE CP-1-I-R:TLAN         %GEN-2F-R:TLAN
WRITE-FILE CP-1-I-R-SUBR:LIST    %GEN-2F-R-SUBR:LIST
WRITE-FILE CP-1-I-R:GDEF         %GRAPHIC DEFINITIONS FOR RTGRAPH

%         ANTENNA POINTED PARALLEL TO MAGNETIC FIELD AT 278.6 KM ALTITUDE
%         ACCORDING TO IGRF MODEL 1985-90 FOR NOV 1986

SET-PARAMETER R1,278.6     % DEFINE INTERSECTION ALTITUDE

&K OFFSET-PPD  620         %SEE TLAN FILE FOR EXPLANATION   2/5 89 KOJ
&S OFFSET-PPD  605
   CHANGE-REF-SITE TROMSO,,,,,
   POINT-REF-H 182.57,77.54,R1

&KSET-SIG-PAT ALLX
&SSET-SIG-PAT ALLX
&KSET-POLAR 94,-8          %SETTINGS FOR THE NOMINAL MIDDLE POINT OF THE
&SSET-POLAR 108,-18        %REMOTE SITE VOLUME AT 277.5 KM ALTITUDE


LOAD-CORR CP-1-I-R:CCOD    %BASING ON GEN-REMOTE; AUG.1986 VERSION
%                          %GEN-2F-R:CCOD
TRACE-ON
SYNC -360
LOAD-RA CP-1-I-R             %GEN-2F-R:TCOD

TRACE-OFF

TRANSFER-NOISE RAD
SET-LO1 1053.5

SET-LO2 3,94.0       % F0
SET-LO2 4,93.0       % F2

&KSET-SIG-ATT X,7            % GaAs-FET AMPLIFIER SET-UP    2/5 89
&SSET-SIG-ATT X,10           % 10.1.89 JM

&KSET-CHAN-ATT 3,3           %BALANCE CHANNELS! LAST: 2/5 89 KOJ
&KSET-CHAN-ATT 4,2
&SSET-CHAN-ATT 3,3           % 15.10.87
&SSET-CHAN-ATT 4,5           %     JM

SYNC 10

SET-FILT 3,BU,25
SET-FILT 4,BU,25


% SET UP CORRELATOR APM AND APB STACKS
% THE DETAILED OPERATIONS CAN BE SEEN FROM THE FILE "CP-1-I-R-SUBR:LIST"
% WHICH IS THE MAIN CORRELATOR PROGRAM.
% THE MASTER PROGRAM IS GEN-REMOTE:CLAN, AUG. 1986 VERSION.

SET-CORR-APB 15,-10    % MARGIN1, OVERLAPPING IN TIMING AND SIGNAL GATES
SET-CORR-APB 14,32     % SIGSAMPLES1, NUMBER OF SAMPLES IN "TRIANGULAR GATES"
SET-CORR-APB 13,29     % LAGMAX1, MAXIMUM LAG, TOTAL NUMBER OF LAGS=30
%      RSAPB(12)       % CALGATES1 , LOADED ACTIVELY IN CP-1-I-R:CCOD
SET-CORR-APB 11,150    % CALPRODUCTS1, THE NUMBER OF PRODUCTS IN "INJECTION/2"

SET-CORR-APB 10,0      % MARGIN2     , LOADING FOR SKIPPING ONLY
SET-CORR-APB 9,0       % SIGSAMPLES2 , LOADING FOR SKIPPING ONLY
SET-CORR-APB 8,29      % LAGMAX2
SET-CORR-APB 7,1       % CALGATES2
SET-CORR-APB 6,300     % CALPRODUCTS2, NUMBER OF PRODUCTS IN A "BOXCAR GATES"
SET-CORR-APB 2,5       % USERREG, NUMBER OF "SIGNAL GATES"
SET-CORR-APB 0,1       % INCREMENT

SET-CORR-APM 15,000    % RESENTRY1, TIMING AND SIGNAL GATES
SET-CORR-APM 14,150    % RESENTRY2, THE FIRST BACKGROUND GATE
SET-CORR-APM 13,180    % RESENTRY3, THE SECOND BACKGROUND GATE
SET-CORR-APM 12,210    % RESENTRY4, THE NOISE INJECTION GATE
SET-CORR-APM 11,240    % RESENTRY5, SCANCOUNTADDRESS
SET-CORR-APM 3,29      % MAXLAG1
SET-CORR-APM 2,29      % MAXLAG2
SET-CORR-APM 0,1       % INCREMENT

SET-CORR-DATAIO 241    % (5+1+1+1)*30+1

SYNC 10

SET-BUF-MEM 3,0
SET-BUF-MEM 4,1136     % THE LAST USED ADDRESS 2271

SET-ADC-INT 3,10.      % 10 US SAMPLES
SET-ADC-INT 4,10.

HOLD 10

TRACE-ON
START-CORR

ENABLE-DMA

SYNC 20

START-R-C 10,1,10

SYNC 10
?
?   -------------------------------------------------------------------
?   TX MIGHT BE ON, CHECK DATA QUALITY. RECORDING STARTS IN 5 MINUTES.
?
?   TO START THE DISPLAY PROGRAM GIVE THE COMMAND '@RTG' FROM GRAPHICS
?   TERMINAL. FOR DOPPLER CALIBRATION GIVE 'GOTO-BLOCK 2' COMMAND
?   -------------------------------------------------------------------
?
SYNC 300

BLOCK 1
DOPPLER-CALIBRATION OFF
START-PROGRAM INTCP,,     % START INTEGRATION PROGRAM FOR GEN-REMOTE
%
SET-PARAMETER I20,1        % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.

TRACE-OFF
DO -1
  NEW-FILE
  DO 3
?   POSITION F:  278.6 KM
    SYNC 60

?   POSITION E1:  90.5 KM
    POINT-REF-HEIGHT 182.57,77.54, 90.5
    SET-PARAMETER I20,2         % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
&K  SET-POLAR 103,-35             %POLARISER SETTING FOR 110 KM ALTITUDE
&S  SET-POLAR 138,-43             %USED IN ALL E-LAYER VOLUMES
    SYNC 90                       % 40+50

?   POSITION E2: 101.1 KM
    POINT-REF-HEIGHT 182.57,77.54,101.1
    SET-PARAMETER I20,3         % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 60                       % 10+50

?   POSITION E3: 116.9 KM
    POINT-REF-HEIGHT 182.57,77.54,116.9
    SET-PARAMETER I20,4         % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 60                       % 10+50

?
?   POSITION F:  278.6 KM
    POINT-REF-HEIGHT 182.57,77.54,R1
    SET-PARAMETER I20,5         % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
&K  SET-POLAR 94,-8               %SETTING BACK TO 278.6 KM VOLUME
&S  SET-POLAR 108,-18
    SYNC 90                       % 30+60


?   POSITION E4:  95.8 KM
    POINT-REF-HEIGHT 182.57,77.54, 95.8
    SET-PARAMETER I20,6         % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
&K  SET-POLAR 103,-35             %POLARISER SETTING FOR 110 KM ALTITUDE
&S  SET-POLAR 138,-43             %USED IN ALL E-LAYER VOLUMES
    SYNC 90                       % 40+50

?   POSITION E5: 109.0 KM
    POINT-REF-HEIGHT 182.57,77.54,109.0
    SET-PARAMETER I20,7         % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 60                       % 10+50

?   POSITION E6: 124.8 KM
    POINT-REF-HEIGHT 182.57,77.54,124.8
    SET-PARAMETER I20,8         % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
    SYNC 60                       % 10+50
?
?   POSITION F:  278.6 KM
    POINT-REF-HEIGHT 182.57,77.54,R1
    SET-PARAMETER I20,1         % AS A MARKER OF THE REMOTE ANTENNA POSITION NO.
&K  SET-POLAR 94,-8               %SETTING BACK TO 278.6 KM VOLUME
&S  SET-POLAR 108,-18
%
    SYNC 30
  ENDDO
ENDDO

BLOCK 2
%
STOP-RECORDING
STOP-PROGRAM INTCP
?
? >>>>>>>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<<<<<<
? THE DOPPLER-CALIBRATION WILL START IN 1 MIN, PLEASE MONITOR
? >>>>>>>>>>>>>>>>>>>>>>>>>>>>>><<<<<<<<<<<<<<<<<<<<<<<<<<<<<
?
TRACE-ON
WRITE-FILE (CP-EI)CP-DOPPLER:DESC
&K SET-SIGNAL-ATTENUATOR  X,20
&S SET-SIGNAL-ATTENUATOR  X,5  %changed from 25 dB 21.9.88 Jm
SET-LO2  3,89.9
SET-LO2  4,89.9
DOPPLER-CALIBRATION ON
SYNC 60
TRACE-OFF
START-RECORDING
DO 2
?  >>> DOPPLER CALIBRATION ON <<<
? TO STOP THE PROGRAM USE "STOP-EXPERIMENT" COMMAND OR WAIT 10 MINUTES
? TO CONTINUE WITH CP-1 USE "GOTO-BLOCK 3"
?
  SYNC 300
ENDDO
DOPPLER-CALIBRATION OFF
STOP-EXPERIMENT,,,,
%
% STANDARD STOP OF EXPERIMENT AFTER DOPPLER-CALIBRATION
%
BLOCK 3
TRACE-ON
STOP-RECORDING
DUMP-LOGG-FILE
%
SET-LO2 3,94.0       % F0
SET-LO2 4,93.0       % F2
%
&KSET-SIG-ATT X,7            % GaAs-FET AMPLIFIER SET-UP    6/9 88
&SSET-SIG-ATT X,10           % 10.1.89 JM
%
DOPPLER-CALIBRATION OFF
%
?  >>> DOPPLER CALIBRATION OFF, PREPARING FOR CP-1 SETTINGS <<
?  TO START CP-1 RECORDING USE "GOTO-BLOCK 1 <HR>,<MIN>,<SEC>"
?
%
% WAIT HERE FOR EITHER STOP-EXPERIMENT OR GOTO-BLOCK 2 (OR 1)
%
DO -1
?   >>> YOU ARE IN BLOCK 3  <<<
?   >> GOTO-BLOCK 1 TO CONTINUE EXPERIMENT OR STOP-EXPERIMENT <<
SYNC 600
ENDDO
%
STOP-EXPERIMENT,,
E-O-E

