%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%%  ***  T A R L A N   P R O G R A M	F O R   T R O M S O   ***
%%
%%  EXPERIMENT: COMMON PROGRAM ONE		VERSION C
%%
%%  USING FRENCH CORRELATOR PROGRAM FOR DOUBLE MULTIPULSE, POWER
%%  PROFILE AND SINGLE PULSE WITH XX,YY AND XY PRODUCTS IN FRONT
%%  OF ACFS.
%%
%%    2 5*15 US MULTIPULSE SEQUENCES FOR E REGION
%%    1 15 US POWER PROFILE FOR D AND E REGION
%%    1 60 US POWER PROFILE FOR E AND LOWER F REGION
%%    2 360 US PULSES, ONE FOR F REGION ACFS FOR TROMSO,
%%		          BOTH USED FOR REMOTE SITE ACFS
%%
%%  TYPE: COMMON PROGRAM.
%%
%%  FILE NAME: (EXP)CP-1-C-T:TLAN
%%
%%  START DELAY: 120 S.
%%
%%  04 JANUARY 1984      SHEILA KIRKWOOD, TROMSO
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
% FIRST THE TRANSMITTING SEQUENCE
%
AT 1 TRANS,CHQPULS
AT 10 RXPROT,LOPROT
AT 40 BEAMON
AT 60 F2		% FREQUENCY SET AT F2 FOR REMOTE SITE PULSE
AT 100 RFON
SETTCR 360		% SETTCR TO THE REQUIRED PULSE LENGTH
AT 100 RFOFF,F13,RFON		% FREQUENCY SET AT F13 FOR SINGLE PULSE (CH7)
AT 460 RFOFF,F6,RFON		% FREQUENCY SET AT F6 FOR FIRST MULTIPULSE (CH3)
AT 475 RFOFF,F1,RFON		% FREQUENCY SET AT F1 FOR SECOND MULTIPULSE (CH4)
AT 490 RFOFF
AT 491 F6
AT 550 RFON
AT 565 RFOFF,F1,RFON
AT 580 RFOFF,F6,RFON
AT 595 RFOFF,F1,RFON
AT 610 RFOFF,F10,RFON		% FREQUENCY SET AT F10 FOR SECOND POWER PROF (CH6)
AT 670 RFOFF
AT 671 F6
AT 730 RFON
AT 745 RFOFF,F1,RFON
AT 760 RFOFF,F4,RFON		% FREQUENCY SET AT F4 FOR FIRST POWER PROFILE (CH5)
AT 775 RFOFF
AT 780 F6
AT 790 RFON
AT 805 RFOFF,F1,RFON
AT 820 RFOFF,BEAMOFF
AT  950 RXPOFF
AT 1095 LOPOFF
%
% NEXT THE RECEIVING SEQUENCE
%
AT 1100 RECEV
AT 1113 CH3
AT 1128 CH4
AT 1173 CH5
AT 1177 CH7
AT 1254 CH6
AT 1770 CH5OFF	% 40 SAMPLES WITH 15US SAMPLING
AT 1800 CH3OFF	% 46 SAMPLES WITH 15US SAMPLING
AT 1815 CH4OFF	% "      "     "	 "     "
AT 2990 CH6OFF	% 174 SAMPLES WITH 10US SAMPLING
AT 4952 CH7OFF	% 378 SAMPLES WITH 10US SAMPLING
%
% AND NOW THE CALIBRATION
%
SETTCR 10000
AT 60 CAL100
AT 100 CH7
AT 460 CH3
AT 475 CH4
AT 610 CH6
AT 760 CH5
AT 785 CH6OFF	% 18 SAMPLES WITH 10US SAMPLING
AT 830 CH3OFF	% 25 SAMPLES WITH 15US SAMPLING
AT 845 CH4OFF	% "      "     "	 "     "
AT 905 CH5OFF	% 10 SAMPLES WITH 15US SAMPLING
AT 1175 CH7OFF,CAL0	% 108 SAMPLES WITH 10US SAMPLING
%
% AND LAST OF ALL THE BACKGROUND
%
SETTCR 11200
AT 100 CH7		% SAME NUMBERS OF SAMPLES AS FOR CALIBRATION
AT 460 CH3
AT 475 CH4
AT 610 CH6
AT 760 CH5
AT 785 CH6OFF
AT 830 CH3OFF
AT 845 CH4OFF
AT 905 CH5OFF
AT 1172 STC
AT 1175 CH7OFF
AT 1210 BUFLIP
SETTCR 0
AT 12500 REP
